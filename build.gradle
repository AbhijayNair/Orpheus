buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'hugo'

repositories {
    maven { url "../m2/repository" }
    mavenCentral()
}

// Build config
ext.compileSdkVersion = 19
ext.buildToolsVersion  = "19.1.0"

// defaultConfig
ext.targetSdkVersion = 19

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def getDebugVersionSuffix() {
    // When building with Android Studio gitSha() is null;
    return "${gitSha()}".isEmpty() ? "-SNAPSHOT" : "-SNAPSHOT-${gitSha()}"
}

// Versioning
def versionMajor = 0
def versionMinor = 5
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

assert(versionMinor < 100) // Double digits
assert(versionPatch < 100) // Double digits
assert(versionBuild < 100) // Double digits

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

//    lintOptions {
//        abortOnError false
//    }

    defaultConfig {
        applicationId "org.opensilk.music"
        minSdkVersion 14
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName String.format("%d.%d.%d", versionMajor, versionMinor, versionPatch)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res','res-new-themes','res-new']
            assets.srcDirs = ['assets']
        }
        debug {
            manifest.srcFile 'flavors/debug/AndroidManifest.xml'
            res.srcDirs = ['flavors/debug/res']
        }
        release {
            manifest.srcFile 'flavors/release/AndroidManifest.xml'
            res.srcDirs = ['flavors/release/res']
        }
    }

    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../KeyStore/release.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
        debug {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../KeyStore/debug.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "${gitSha()}".isEmpty() ? "-SNAPSHOT" : "-SNAPSHOT-${gitSha()}"
            buildConfigField "String", "MUSIC_AUTHORITY", "\"org.opensilk.music.provider.debug\""
            buildConfigField "String", "ARTWORK_AUTHORITY", "\"org.opensilk.music.artwork.provider.debug\""
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "String", "MUSIC_AUTHORITY", "\"org.opensilk.music.provider\""
            buildConfigField "String", "ARTWORK_AUTHORITY", "\"org.opensilk.music.artwork.provider\""
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    useOldManifestMerger true

}

//configurations.all {
//    // check for updates every build
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}

dependencies {
    compile 'com.android.support:support-v4:19.+'
    compile 'com.github.gabrielemariotti.cards:library:1.7.3'
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'com.android.support:mediarouter-v7:19.+'
    compile 'com.google.android.gms:play-services:4.2.+'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'commons-io:commons-io:2.4'
    compile 'com.github.gabrielemariotti.changeloglib:library:1.4.0'
    compile 'com.google.android.apps.muzei:muzei-api:+'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.jakewharton:butterknife:5.1.0'
    compile 'com.jakewharton.timber:timber:2.3.0'

    provided 'com.squareup.dagger:dagger-compiler:1.2.+'
    compile 'com.squareup.dagger:dagger:1.2.+'
    compile 'org.opensilk.silkdagger:libsilkdagger:0.2.2-SNAPSHOT@aar'

    compile 'com.sothree.slidinguppanel:library:1.0.0-SNAPSHOT@aar'
    compile 'com.mobeta.android.dslv:library:0.6.1-SNAPSHOT@aar'
    compile 'com.github.manuelpeinado.fadingactionbar:fadingactionbar:3.1.2-SNAPSHOT@aar'
    compile 'com.github.manuelpeinado.fadingactionbar:fadingactionbar-abc:3.1.2-SNAPSHOT@aar'
    compile 'com.android.volley:volley:1.0-SNAPSHOT@aar'

    compile project(':../SilkCastService:library')
    compile project(':../OrpheusApi:api')
}