buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.0.1'
    }
}
apply plugin: 'android'
apply plugin: 'hugo'

repositories {
    mavenCentral()
}

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 2 // bump for dogfood builds, public betas, etc.

assert(versionMinor < 10) // Single digit
assert(versionPatch < 100) // Double digits
assert(versionBuild < 100) // Double digits

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

//    lintOptions {
//        abortOnError false
//    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode versionMajor * 100000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName String.format("%d.%d.%02d", versionMajor, versionMinor, versionPatch)
        packageName "org.opensilk.music"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res','res-new']
            assets.srcDirs = ['assets']
        }
        debug {
            manifest.srcFile 'flavors/debug/AndroidManifest.xml'
            res.srcDirs = ['flavors/debug/res']
        }
        release {
            manifest.srcFile 'flavors/release/AndroidManifest.xml'
        }
    }

    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../KeyStore/release.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
        debug {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../KeyStore/debug.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix "${gitSha()}".isEmpty() ? "-SNAPSHOT" : "-SNAPSHOT-${gitSha()}"
            buildConfigField "String", "FLAVORED_AUTHORITY", "\"org.opensilk.music.provider.debug\""
            buildConfigField "String", "CAST_ID", "\"25A19AAC\""
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "String", "FLAVORED_AUTHORITY", "\"org.opensilk.music.provider\""
            buildConfigField "String", "CAST_ID", "\"FBF2F70D\"";
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

}

dependencies {
    compile 'com.android.support:support-v4:19.0.1'
    compile project(':AndroidSlidingUpPanel')
    compile 'com.github.gabrielemariotti.cards:library:1.3.0'
    compile project(':drag-sort-listview')
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.android.support:mediarouter-v7:19.0.1'
    compile 'com.google.android.gms:play-services:4.2.+'
    compile project(':FadingActionBar')
}